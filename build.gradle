plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.codemc.io/repository/nms/' }
    jcenter()
    mavenLocal()
}

defaultTasks("shadowjar")

dependencies {
    compileOnly 'org.bukkit:craftbukkit:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.sct.vault:VaultAPI:1.7'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

group = 'org.sct'
version = '2.4.2'
sourceCompatibility = '8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Xlint:deprecation')
    options.compilerArgs.add('-Xlint:unchecked')
}

task copyFile(type: Copy, dependsOn: shadowJar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from('build/libs/')
    into('E:/work/1.15.2test/plugins/')
    include('EasyLib*.jar')
    rename {
        "EasyLib.jar"
    }
}

bintray {
    user = 'lovesasuna'
    key = System.getenv("JcenterKey")
    publications = ['plugin']
    pkg {
        repo = 'maven'
        name = 'EasyLib'
        licenses = ['LPGL-3.0']
        vcsUrl = 'https://github.com/LovesAsuna/EasyLib.git'
        version {
            name = project.version
            desc = 'Minecraft Plugin Dependency'
            released = new Date()
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            description 'Minecraft Plugin Dependency'
            groupId = 'me.lovesasuna'
            pom {
                developers {
                    developer {
                        name = 'LovesAsuna'
                    }
                }
                licenses {
                    license {
                        name = 'LGPL-3.0'
                        url = 'https://github.com/LovesAsuna/EasyLib/raw/master/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/LovesAsuna/EasyLib'
                }
            }
        }
    }
}