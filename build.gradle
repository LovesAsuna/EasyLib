plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.dokka' version '1.4.30'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.codemc.io/repository/nms/' }
    mavenLocal()
}

defaultTasks("shadowjar")

dependencies {
    compileOnly 'org.bukkit:craftbukkit:1.15.2-R0.1-SNAPSHOT'
    implementation 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.sct.vault:VaultAPI:1.7'
    compileOnly 'com.fasterxml.jackson.module:jackson-module-kotlin:2.12.3'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

group = 'com.hyosakura'
version = '1.0.0'
sourceCompatibility = '8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-Xlint:deprecation')
    options.compilerArgs.add('-Xlint:unchecked')
}

task dokka(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'documentation'
    classifier("javadoc")
    from javadoc.destinationDir
}

dokkaJavadoc {
    outputDirectory = javadoc.destinationDir
}

javadoc {
    dependsOn('dokka')
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'easylib'
            version = version
            artifact dokka
            from components.java

            pom {
                name = 'EasyLib'
                description = 'Minecraft Plugin Dependency'
                url = 'https://github.com/LovesAsuna/EasyLib'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'LovesAsuna'
                        email = 'qq625924077@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/LovesAsuna/EasyLib'
                    developerConnection = 'scm:git:https://github.com/LovesAsuna'
                    url = 'https://github.com/LovesAsuna/EasyLib'
                }
            }
        }
    }
    repositories {
        maven {
            name 'release'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
